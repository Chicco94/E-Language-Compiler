-- Position to ident
position token PIdent (letter (letter|digit|'_'|'\'')*) ;

-- A program is a sequence of declarations.
PDefs.     Program ::= [Decl] ;
terminator Decl "" ;

-- A program may containt (C-like) comments, which are ignored by the parser.
comment "//" ;
comment "/*" "*/" ;

-- Function declaration. E.g.: def foo (int x, double y) : int { }
DeclFun.   Decl ::= "def" LExpr "(" [Arg] ")" Guard "{" [Stmt] "}" ;
separator  Arg "," ;
terminator Stmt "" ;

-- Guards of declarations. E.g.: 
-- def i : <guard>; 
-- def foo (i : <guard>) : <guard> {}
GuardVoid.  Guard ::= {-empty-} ;  -- empty guard
GuardType.  Guard ::= ":" Type ;   -- guarded

-- An argument declaration has a type and an identifier.
ArgDecl.   Arg ::= PIdent Guard ;

-- Any Expression followed by a semicolon ; can be used as a statement.
StmtExpr.       Stmt     ::= Expr EndLine ;

--StmtDecl.       Stmt     ::= "var" LExpr Guard EndLine ;
--StmtInit.       Stmt     ::= "var" LExpr Guard ":=" Expr EndLine ;
--StmtArrDecl.    Stmt     ::= "def" LExpr Guard ":=" TypeIter EndLine ;

StmtDecl.        Stmt     ::= "var" LExpr Guard EndLine ;                      -- Variable declaration.
StmtIterDecl.    Stmt     ::= "var" "[" LExpr "]" Guard EndLine ;              -- Iterable type declaration.
StmtVarInit.     Stmt     ::= "var" LExpr Guard ":=" Expr EndLine ;     -- Constant/variable initialization.
StmtDefInit.     Stmt     ::= "def" LExpr Guard ":=" Expr EndLine ;
StmtVarIterInit. Stmt     ::= "var" LExpr Guard ":=" TypeIter EndLine ; -- Constant/variable iterable type initialization.
StmtDefIterInit. Stmt     ::= "def" LExpr Guard ":=" TypeIter EndLine ;
--DeclModVar.      DeclModality ::= "var" ;
--DeclModDef.      DeclModality ::= "def" ;

StmtReturn.     Stmt     ::= "return" [Expr] EndLine ;

StmtBlock.      Stmt     ::= "{" [Decl] "}" ;

StmtIfElse.     Stmt     ::= "if" "(" Expr ")" Stmt "else" Stmt ;
StmtIfNoElse.   Stmt     ::= "if" "(" Expr ")" Stmt ;
SSwitchCase.    Stmt     ::= "switch" "(" Expr ")" "{" [NormCase] [DfltCase] "}" ;
CaseNormal.     NormCase ::= "match" Expr Stmt ;
CaseDefault.    DfltCase ::= "match _" Stmt ;
separator NormCase "" ;
separator DfltCase "" ;
StmtBreak.      Stmt     ::= "break" ;
StmtContinue.   Stmt     ::= "continue" ;

StmtWhile.      Stmt     ::= "while" "(" Expr ")" Stmt ;
StmtFor.        Stmt     ::= "for" PIdent "in" TypeIter Stmt ;


-- A statement is a declaration.
DeclStmt.       Decl ::= Stmt ;

-- (Left) Expressions
LExprId.        LExpr ::= PIdent ;
LExprDeref.     LExpr ::= Deref ;
LExprRef.       LExpr ::= Ref ;
DerefExpr.      Deref ::= "&" LExpr;
RefExpr.        Ref   ::= "*" LExpr;

-- (Right) Expressions
LExpr.        Expr15 ::= LExpr ;
ExprInt.      Expr15 ::= Integer ;
ExprDouble.   Expr15 ::= Double ;
ExprChar.     Expr15 ::= Char ;
ExprString.   Expr15 ::= String ;
ExprTrue.     Expr15 ::= "true" ;
ExprFalse.    Expr15 ::= "false" ;

ExprFunCall.  Expr15 ::= PIdent "(" [Arg] ")" ;

ExprPower.    Expr13 ::= Expr14 "**"  Expr13 ;

ExprMul.      Expr12 ::= Expr12 "*"  Expr13 ;
ExprFloatDiv. Expr12 ::= Expr12 "/"  Expr13 ;
ExprIntDiv.   Expr12 ::= Expr12 "//" Expr13 ;
ExprReminder. Expr12 ::= Expr12 "%"  Expr13 ;
ExprModulo.   Expr12 ::= Expr12 "%%" Expr13 ;

ExprNegation.  Expr11 ::= "-" Expr12 ;
--ExprReference. Expr11 ::= "&" LExpr ;
ExprPlus.      Expr11 ::= Expr11 "+"  Expr12 ;
ExprMinus.     Expr11 ::= Expr11 "-"  Expr12 ;

--ExprIntInc.   Expr10 ::= Expr10 ".."  Expr11 ;
--ExprIntExc.   Expr10 ::= Expr10 "..!" Expr11 ;

ExprLt.       Expr9  ::= Expr9  "<"   Expr10 ;
ExprGt.       Expr9  ::= Expr9  ">"   Expr10 ;
ExprLtEq.     Expr9  ::= Expr9  "<="  Expr10 ;
ExprGtEq.     Expr9  ::= Expr9  ">="  Expr10 ;

ExprEq.       Expr8  ::= Expr8  "=="  Expr9 ;
ExprNeq.      Expr8  ::= Expr8  "!="  Expr9 ;

ExprBoolNot.  Expr5  ::= "!" Expr6 ;

ExprAnd.      Expr4  ::= Expr4  "&&"  Expr5 ;

ExprOr.       Expr3  ::= Expr3  "||"  Expr4 ;

--ExprAssign.   Expr2  ::= Expr3  ":="  Expr2 ;
ExprAssign.   Expr2  ::= LExpr  AssignOperator  Expr2  ;
coercions Expr 15 ;
separator Expr "," ;

-- Assignment operators (i.e., l-value operator r-value).
OpAssign.     AssignOperator ::= ":=" ;
OpOr.         AssignOperator ::= "|=" ;
OpAnd.        AssignOperator ::= "&=" ;
OpPlus.       AssignOperator ::= "+=" ;
OpMinus.      AssignOperator ::= "-=" ;
OpMul.        AssignOperator ::= "*=" ;
OpIntDiv.     AssignOperator ::= "//=" ;
OpFloatDiv.   AssignOperator ::= "/=" ;
OpRemainder.  AssignOperator ::= "%=" ;
OpModulo.     AssignOperator ::= "%%=" ;
OpPower.      AssignOperator ::= "**=" ;

-- The available type are bool, double, int, void, char and string.
TypeBool.   Type ::= "bool" ;
TypeDouble. Type ::= "double" ;
TypeInt.    Type ::= "int" ;
TypeVoid.   Type ::= "void" ; 
TypeChar.   Type ::= "char" ;
TypeString. Type ::= "string" ; 
--TypeAny.    Type ::= "any" ;

-- Compound types.
TypeCompound.     Type ::= CompoundType ;
TypePointer.      CompoundType ::= Type "*" ; 
TypeIterable.     CompoundType ::= TypeIter ;
TypeIterIntInc.   TypeIter     ::= Expr ".." Expr ;   -- Iterable inclusive interval.
TypeIterIntExc.   TypeIter     ::= Expr "..!" Expr ;  -- Iterable exclusive interval.
TypeIterArray.    TypeIter     ::= "[" [Expr] "]" ;   -- Iterable array.


-- End statement character.
Semicolon. EndLine ::= ";" ;
--NewLine.   EndLine ::= "\n" ;

-- The entry point is the program.
entrypoints Program ;

-- Include in the abstract syntax a type annotated version of expressions.
internal ExprTyped. Expr ::= "[" Type ":]" Expr ;


